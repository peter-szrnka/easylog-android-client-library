plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    id("maven-publish")
}

android {
    namespace = "io.github.easylog.client"
    compileSdk = 36

    defaultConfig {
        minSdk = 26

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles("consumer-rules.pro")
    }

    buildTypes {
        release {
            isMinifyEnabled = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }

    publishing {
        singleVariant("release")
    }
}

dependencies {
    implementation(libs.androidx.core.ktx)
    implementation("io.github.easylog:common:0.0.1-SNAPSHOT")
    implementation("com.squareup.okhttp3:okhttp:4.12.0")
    implementation("com.google.code.gson:gson:2.13.2")

}

tasks.register<Exec>("buildServerCommon") {
    workingDir = file("server")
    commandLine("mvn", "clean", "install", "-DskipTests", "-Ponly-common")
}


publishing {
    publications {
		register<MavenPublication>("release") {
            groupId = "io.github.easylog"
            artifactId = "android-client-library"
version = "0.0.2-SNAPSHOT"

            afterEvaluate {
				val releaseComponent = components.findByName("release")
                if (releaseComponent != null) {
					println("Release component found")
                    from(releaseComponent)
                } else {
                    println("No release component found â€“ AAR will not be published!")
                }
            }
        }
    }
}

tasks.withType<PublishToMavenLocal>().configureEach {
    onlyIf { publication.name == "release" }
}